package com.kii.extension.ruleengine.drools.rule

import com.kii.extension.ruleengine.drools.entity.*;
import com.kii.extension.ruleengine.store.trigger.TriggerGroupPolicyType;



rule "summary of  true  result:all"
when
	$trigger:Trigger(policy ==TriggerGroupPolicyType.All,$triggerID:triggerID,$things:things)
    CurrThing(thing memberOf $things ||  init==true )
	accumulate(MemberMatchResult(triggerID==$triggerID,$thingID:thingID);
                        $thingCount : count($thingID);
                        $thingCount==$trigger.things.size()
              )
then
	System.out.println("group all trigger all "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end


rule "summary of  true  result:any "
when
	$trigger:Trigger(policy ==TriggerGroupPolicyType.Any,$triggerID:triggerID,$things:things)
    CurrThing(thing memberOf $things || init == true)
	accumulate( MemberMatchResult(triggerID==$triggerID,thingID memberOf  $things, $thingID:thingID );
                      $thingCount : count($thingID);
					  $thingCount>0
                  )
then
	System.out.println("group any trigger  "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end

rule "summary of  true  result:number"
when
	$trigger:Trigger(policy ==TriggerGroupPolicyType.Some,$triggerID:triggerID,$things:things)
    CurrThing(thing memberOf $things|| init == true)
	accumulate( MemberMatchResult(triggerID==$triggerID,$thingID:thingID );
                    $thingCount : count($thingID);
                    $thingCount>=$trigger.getNumber()
                    )
then
	System.out.println("group trigger number  "+$trigger.getTriggerID()+" been fire:"+$thingCount);
	insertLogical(new MatchResult($triggerID));
end


rule "summary of  true  result:percent"
when
	$trigger:Trigger(policy ==TriggerGroupPolicyType.Percent,$triggerID:triggerID,$things:things)
    CurrThing(thing memberOf $things|| init == true)
	accumulate( MemberMatchResult(triggerID==$triggerID,$thingID:thingID );
                    $thingCount : count($thingID);
                    $thingCount>=$trigger.getNumber()*$things.size()/100
                    )
then
	System.out.println("group trigger percent  "+$trigger.getTriggerID()+" been fire:"+$thingCount);
	insertLogical(new MatchResult($triggerID));
end