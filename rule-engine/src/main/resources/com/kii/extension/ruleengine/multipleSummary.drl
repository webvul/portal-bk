package com.kii.extension.ruleengine.drools.rule

import com.kii.extension.ruleengine.drools.entity.*;
import com.kii.extension.ruleengine.ExtendFunAdapter;

global com.kii.extension.ruleengine.ExtendFunAdapter ExtFun;


rule "copy  all multiples's unit summary value to multipleMap"
  no-loop true
when
	Trigger(type ==TriggerType.multiple , $triggerID:triggerID)
	$summary:SummaryResult(triggerID==$triggerID)
	$thing:ThingResult(triggerID==$triggerID)
	$multiplesMap:MultiplesValueMap(triggerID==$triggerID)
then
	modify($multiplesMap){
    	setSummaryValue($summary),
    	setThingValue($thing)
    };
end



rule "in unit,compute the  summary of  fieldName value: funName"
when
	$trigger:Trigger(type==TriggerType.multiple,$triggerID:triggerID)
	$summary:Summary(triggerID==$triggerID, funName=="sum" ,$name:name,$things:things)
	accumulate( $status : MemberMatchResult(thingID memberOf $things , name==$name);
                    $sum : sum($status.getNumValue($summary.getFieldName()))
                  )
then
	insert(new SummaryResult($triggerID,$name,$sum));
end


rule "in unit,compute the  count of  fieldName value: funName"
when
	$trigger:Trigger(type==TriggerType.multiple,$triggerID:triggerID)
	$summary:Summary(triggerID==$triggerID, funName=="count" ,$name:name,$things:things)
	accumulate( $status : MemberMatchResult(thingID memberOf $things ,name==$name);
                    $val : count($status)
                  )
then
	insert(new SummaryResult($triggerID,$name,$val));
end

rule "in unit,compute the  max of  fieldName value: funName"
when
	$trigger:Trigger(type==TriggerType.multiple,$triggerID:triggerID)
	$inst:TriggerValues(triggerID==$triggerID)
	$summary:Summary(triggerID==$triggerID, funName=="max" ,$name:name,$things:things)
	accumulate( $status : MemberMatchResult(thingID memberOf $things , name==$name);
                    $val : max($status.getNumValue($summary.getFieldName()))
                  )
    MemberMatchResult( getNumValue($summary.fieldName) == $val,$choiceTh:thingID)
then
	System.out.println("max summary result:"+$summary);
	ExternalValues entity=new ExternalValues($name);
    entity.addValue("choiceTh",$choiceTh);
	$inst.updateEntity(entity);

	insert(new SummaryResult($triggerID,$name,$val));
end

rule "in unit,compute the  min of  fieldName value: funName"
when
	$trigger:Trigger(type==TriggerType.multiple,$triggerID:triggerID)
	$inst:TriggerValues(triggerID==$triggerID)
	$summary:Summary(triggerID==$triggerID, funName=="min" ,$name:name,$things:things)
	accumulate( $status : MemberMatchResult(thingID memberOf $things , name==$name);
                    $val : min($status.getNumValue($summary.getFieldName()))
                  )
    MemberMatchResult(getNumValue($summary.fieldName) == $val,$choiceTh:thingID)
then
	System.out.println("min summary result:"+$val);
	ExternalValues entity=new ExternalValues($name);
    entity.addValue("choiceTh",$choiceTh);
	$inst.updateEntity(entity);
	insert(new SummaryResult($triggerID,$name,$val));
end


rule "in unit,compute the  avg of  fieldName value: funName"
when
	$trigger:Trigger(type==TriggerType.multiple,$triggerID:triggerID)
	$summary:Summary(triggerID==$triggerID, funName=="average" ,$name:name,$things:things)
	accumulate( $status : MemberMatchResult(thingID memberOf $things , name ==$name );
                    $val : average($status.getNumValue($summary.getFieldName()))
                  )
then
	insert(new SummaryResult($triggerID,$name,$val));
end