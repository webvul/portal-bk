package com.kii.extension.ruleengine.drools.rule

import com.kii.extension.ruleengine.drools.entity.*;
import com.kii.extension.ruleengine.drools.CommandExec;
import com.kii.extension.ruleengine.store.trigger.WhenType;


query "get Match Result by TriggerID" ()
	Trigger($triggerID:triggerID,when==WhenType.CONDITION_TRUE,enable==true)
    results : MatchResult( triggerID == $triggerID )
end

rule "fire all trigger:when false->true,trigger  fire"
when
	$trigger:Trigger($triggerID:triggerID,when == WhenType.CONDITION_FALSE_TO_TRUE)
    $result:MatchResult(triggerID==$triggerID)
    not ( TriggerResult(triggerID==$triggerID) )
then
	System.out.println("false-true "+$triggerID+" been fired");
	insert(new TriggerResult($triggerID));
	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecute($triggerID,$result);
end


rule "fire all trigger:when false->true,trigger not fire"
when
	$trigger:Trigger($triggerID:triggerID,when == WhenType.CONDITION_FALSE_TO_TRUE )
    not ( exists MatchResult(triggerID==$triggerID) )
    $result:TriggerResult(triggerID==$triggerID)
then
	System.out.println("false-true "+$triggerID+" not been fired");
	delete($result);
end

rule "fire all trigger:when false->true change,trigger  fire"
when
	$trigger:Trigger($triggerID:triggerID, when == WhenType.CONDITION_CHANGED)
    $result:MatchResult(triggerID==$triggerID)
    not ( TriggerResult(triggerID==$triggerID) )
then
	System.out.println("change "+$triggerID+" been fired:false>true");
	insert(new TriggerResult($result));
	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecute($triggerID,$result);
end

rule "fire all trigger:when true->false change,trigger  fire"
when
	$trigger:Trigger($triggerID:triggerID, when == WhenType.CONDITION_CHANGED)
    not (MatchResult(triggerID==$triggerID))
    $result:TriggerResult(triggerID==$triggerID)
then
	System.out.println("change "+$triggerID+" been fired:true->false");
	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecute($triggerID,$result.getMatchResult());
    delete($result);

end


rule "fire trigger:when true->false,trigger fire "
when
	$trigger:Trigger($triggerID:triggerID ,when == WhenType.CONDITION_TRUE_TO_FALSE  )
    not( MatchResult(triggerID==$triggerID) )
    $result:TriggerResult(triggerID==$triggerID)
then
	System.out.println("true-false "+ $triggerID +" fired ");
    CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecute($triggerID,$result.getMatchResult());
    delete($result);

end


rule "fire trigger:when true->false,trigger not fire "
when
	$trigger:Trigger($triggerID:triggerID,when == WhenType.CONDITION_TRUE_TO_FALSE )
    $result:MatchResult(triggerID==$triggerID)
    not ( TriggerResult(triggerID==$triggerID) )
then
	System.out.println("true-false "+ $triggerID +" not fired");
	insert(new TriggerResult($result));
end