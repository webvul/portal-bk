package com.kii.extension.ruleengine.drools.rule

import com.kii.extension.ruleengine.drools.entity.*;
import com.kii.extension.ruleengine.drools.CommandExec;
import com.kii.extension.ruleengine.store.trigger.WhenType;
import com.kii.extension.ruleengine.ExtendFunAdapter;
import com.kii.extension.ruleengine.ExtendFunAdapter;

global com.kii.extension.ruleengine.ExtendFunAdapter ExtFun;


//query "get Match Result by TriggerID" ()
//	Trigger($triggerID:triggerID,$thingSet:thingSet,when==WhenType.CONDITION_TRUE,enable==true)
//	CurrThing( valid ($thingSet,$triggerID) )
//    results : MatchResult( triggerID == $triggerID )
//end

rule "compute the  collect of  matchResult"
  no-loop true
when
	Trigger($triggerID:triggerID,$thingSet:thingSet,when==WhenType.CONDITION_TRUE,enable==true)
	AtomicCurrThing( valid($thingSet,$triggerID) )
	accumulate( $result : MatchResult( triggerID == $triggerID );
                    $val : collectSet($result)
                  )
then
	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecuteSet($val);
end


rule "fire f2t trigger:when false->true,trigger  fire"
when
	$trigger:Trigger($triggerID:triggerID,$thingSet:thingSet,when == WhenType.CONDITION_FALSE_TO_TRUE)
    $result:MatchResult(triggerID==$triggerID)
    $curr:AtomicCurrThing( valid($thingSet,$triggerID) )

    not ( TriggerResult(triggerID==$triggerID) )
then
	insert(new TriggerResult($triggerID));
	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecute($triggerID,$result);
end


rule "fire f2t trigger:when false->true,trigger not fire"
when
	$trigger:Trigger($triggerID:triggerID,$thingSet:thingSet,when == WhenType.CONDITION_FALSE_TO_TRUE )
    not ( exists MatchResult(triggerID==$triggerID) )
    $curr:AtomicCurrThing( valid($thingSet,$triggerID) )
    $result:TriggerResult(triggerID==$triggerID)
then
	delete($result);
end

rule "fire change trigger:when false->true change,trigger  fire"
when
	$trigger:Trigger($triggerID:triggerID,$thingSet:thingSet, when == WhenType.CONDITION_CHANGED)
	$curr:AtomicCurrThing( valid($thingSet,$triggerID) )
    $result:MatchResult(triggerID==$triggerID)
    not ( TriggerResult(triggerID==$triggerID) )
then
	insert(new TriggerResult($result));

	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    $result.setResult(true);
    exec.doExecute($triggerID,$result);
end

rule "fire change trigger:when true->false change,trigger  fire"
when
	$trigger:Trigger($triggerID:triggerID,$thingSet:thingSet, when == WhenType.CONDITION_CHANGED)
	$curr:AtomicCurrThing( valid($thingSet,$triggerID) )
    not (MatchResult(triggerID==$triggerID))
    $result:TriggerResult(triggerID==$triggerID)
then
	CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
	MatchResult match=$result.getMatchResult();
	match.setResult(false);
    exec.doExecute($triggerID,match);
    delete($result);

end


rule "fire t2f trigger:when true->false,trigger fire "
when
	$trigger:Trigger($triggerID:triggerID ,$thingSet:thingSet,when == WhenType.CONDITION_TRUE_TO_FALSE  )
	$curr:AtomicCurrThing(valid($thingSet,$triggerID))
    not (MatchResult(triggerID==$triggerID))
    $result:TriggerResult(triggerID==$triggerID)
then
    CommandExec exec=(CommandExec)kcontext.getKieRuntime().getEnvironment().get("exec");
    exec.doExecute($triggerID,$result.getMatchResult());
    delete($result);

end


rule "fire t2f trigger:when true->false,trigger not fire "
when
	$trigger:Trigger($triggerID:triggerID,$thingSet:thingSet,when == WhenType.CONDITION_TRUE_TO_FALSE )
	$curr:AtomicCurrThing( valid($thingSet,$triggerID) )
    $result:MatchResult(triggerID==$triggerID)
    not ( TriggerResult(triggerID==$triggerID) )
then
	insert(new TriggerResult($result));
end