package com.kii.extension.ruleengine.drools.rule

import com.kii.extension.ruleengine.drools.entity.*;
import com.kii.extension.ruleengine.store.trigger.groups.TriggerGroupPolicyType;



rule "summary of  true  result:all"
when
	$trigger:Trigger($triggerID:triggerID,type==TriggerType.group)
	Group($thingIDs:thingIDs, policy ==TriggerGroupPolicyType.All,triggerID==$triggerID )
    CurrThing(thing memberOf $thingIDs ||  init==true )
	accumulate(MemberMatchResult(triggerID==$triggerID);
                        $thingCount : count(1);
                        $thingCount==$thingIDs.size()
              )
then
	System.out.println("group all trigger all "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end


rule "summary of  true  result:any "
when
	$trigger:Trigger($triggerID:triggerID,type==TriggerType.group)
	Group(triggerID==$triggerID,$thingIDs:thingIDs,policy ==TriggerGroupPolicyType.Any )
    CurrThing(thing memberOf $thingIDs || init == true)
	accumulate( MemberMatchResult(triggerID==$triggerID,thingID memberOf  $thingIDs );
                      $thingCount : count(1);
					  $thingCount>0
                  )
then
	System.out.println("group any trigger  "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end

rule "summary of  true  result:number"
when
	$trigger:Trigger($triggerID:triggerID,type==TriggerType.group)
	Group(triggerID==$triggerID,$thingIDs:thingIDs,policy ==TriggerGroupPolicyType.Some,$number:number )
    CurrThing(thing memberOf $thingIDs|| init == true)
	accumulate( MemberMatchResult(triggerID==$triggerID,thingID memberOf $thingIDs);
                    $thingCount : count(1);
                    $thingCount>=$number
                    )
then
	System.out.println("group trigger number  "+$trigger.getTriggerID()+" been fire:"+$thingCount);
	insertLogical(new MatchResult($triggerID));
end


rule "summary of  true  result:percent"
when
	$trigger:Trigger($triggerID:triggerID,type==TriggerType.group)
	Group(triggerID==$triggerID,$thingIDs:thingIDs,policy ==TriggerGroupPolicyType.Percent,$number:number )
    CurrThing(thing memberOf $thingIDs|| init == true)
	accumulate( MemberMatchResult(triggerID==$triggerID,thingID memberOf $thingIDs );
                    $thingCount : count(1);
                    $thingCount>=$number*$thingIDs.size()/100
                    )
then
	System.out.println("group trigger percent  "+$trigger.getTriggerID()+" been fire:"+$thingCount);
	insertLogical(new MatchResult($triggerID));
end