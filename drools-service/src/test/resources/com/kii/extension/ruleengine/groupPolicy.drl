package com.kii.extension.ruleengine.demo

import com.kii.extension.ruleengine.thingtrigger.*;

rule "compute the summary of  fieldName value:sum funName"
	no-loop true
when
	$trigger:Trigger(type =="summary")
	$summary:Summary(triggerID==$trigger.triggerID , funName=="sum" )
	accumulate( ThingStatus(thingID memberOf $trigger.things ,$status:values);
                    $value : sum($status.get($summary.getFieldName()))
                    )
    $map:SummaryValueMap(triggerID==$trigger.triggerID)
then
	modify($map){
		setNumber($summary.getSummaryField(),$value);
	};
	System.out.println("compute summary:"+$map.getNumberMap());
end


rule "summary of  true  result:all"
when
	$trigger:Trigger(type =="all",$triggerID:triggerID)
	forall( MemberMatchResult(triggerID==$triggerID) )
then
	System.out.println("trigger all "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end


rule "summary of  true  result:any"
when
	$trigger:Trigger(type =="any",$triggerID:triggerID)
	exists MemberMatchResult(triggerID==$triggerID)
then
	System.out.println("trigger any "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end

rule "summary of  true  result:number"
when
	$trigger:Trigger(type =="number",$triggerID:triggerID)
	accumulate( MemberMatchResult(triggerID==$triggerID,$thingID:thingID );
                    $thingCount : count($thingID);
                    $thingCount>=$trigger.number
                    )
then
	System.out.println("trigger number  "+$trigger.getTriggerID()+" been fire");
	insertLogical(new MatchResult($triggerID));
end
